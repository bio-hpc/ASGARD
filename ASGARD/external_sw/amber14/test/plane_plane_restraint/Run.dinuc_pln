#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

echo "SANDER: Dinucleoside restrained with new plane-plane angle"
echo "        restraint that was defined with new natural "
echo "        language restraint input."

if( ! $?TESTsander ) set TESTsander = "../../bin/sander"

if( ! $?DO_PARALLEL ) then
        setenv DO_PARALLEL " "
else
  set numprocs=`echo $DO_PARALLEL | awk -f ../numprocs.awk `
  if ( $numprocs > 2 ) then
      echo " DO_PARALLEL set to $DO_PARALLEL"
      goto toomany
  else if ( $?MP_PROCS)then
      if ($MP_PROCS > 2)then
#               --- For IBM AIX ---
          echo "    MP_PROCS set to "$MP_PROCS
          goto toomany
      endif
  endif
endif

cat > gbin <<EOF
 Plane-Point Test w/ an isolated AU pair
 &cntrl                                                                        
   nstlim=10, cut=99.0, igb=1, saltcon=0.1,
   ntpr=1, ntwr=100000, ntt=3, gamma_ln=0.0,
   ntx=5, irest=1, 
   ntc=2, ntf=2, tol=0.000001,
   dt=0.001, ntb=0,
   nmropt=1,
 /
 &wt type='DUMPFREQ', istep1=1  /
 &wt type='END'   /
DISANG=dinuc_pln.rst
DUMPAVE=dinuc_pln_vs_t
LISTIN=POUT
LISTOUT=POUT
EOF

$DO_PARALLEL $TESTsander -O -i gbin -c dinucAU_in.restrt -o mdout.dinucAU_pln -p dinucAU.prmtop < /dev/null || goto error

../dacdif mdout.dinucAU_pln.save mdout.dinucAU_pln
../dacdif dinuc_pln_vs_t.save dinuc_pln_vs_t
/bin/rm -f restrt mdinfo gbin
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)

toomany:
    echo " too many processors for this test, exiting (Max = 2)"
    echo "============================================================"
    exit(0)

