#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

../../check_GAMESS.x
if( $status > 0) then
  exit(0)
endif

set sander = "../../../bin/sander"
if( $?TESTsander ) then
    set sander = $TESTsander
endif

if( ! $?DO_PARALLEL ) then
    setenv DO_PARALLEL " "
else
    echo "This test not set up for parallel"
    echo "Parallelism handled within GAMESS"
    exit 0
endif

# check on how many CPU core GAMESS shall be running
if ( $?GMS_NCPUS) then
    if ( $GMS_NCPUS > 1 ) then
	echo "GAMESS will run on $GMS_NCPUS CPU cores"
    endif
else
    set GMS_NCPUS = 1
    echo "GAMESS will run on 1 CPU core"
    echo "(environment variable GMS_NCPUS not set)"
endif

cat > mdin <<EOF
2 steps NVE pure QM MD with ADF (LDA/SZ) (dt=0.5fs, no shake)
 &cntrl
  imin   = 0,           !no minimization
  irest  = 1,           !restart
  ntx    = 5,           !coordinates and velocities are read in
  cut    = 9999.9       !non-bonded interactions cutoff
  dt     = 0.0005,      !0.5fs time step
  ntb    = 0,           !no periodicity and PME off!
  ntt    = 0,           !turn off thermostat
  ntpr   = 1,           !print details to log every step (every .5fs)
  ntwx   = 1,           !write coordinates to mdcrd every step (every .5fs)
  ntwr   = 2,           !write restart file at last step
  nstlim = 2,           !run for 2 steps (1 fs at dt=0.5fs)
  nscm   = 0,           !No removal of COM motion,
  ioutfm = 1,           !NetCDF MDCRD.
  ifqnt  = 1,
 /
 &qmmm
  qmmask    = '@*',
  qmcharge  = 0,
  qm_theory = 'EXTERN',
  verbosity = 0,
 /
 &gms
  gms_version = '00',
  num_threads = $GMS_NCPUS,
  basis = '6-31G',
  scf_conv = 1.0D-05,
  chelpg = 1,
  mwords = 50,
  dipole = 1,
 /
EOF

set testname = h2o.bp86_6-31g_md
set output = $testname.out

touch dummy
$DO_PARALLEL $sander -O -p h2o.prmtop -c h2o.restrt -o $output < dummy || goto error
# remove info about number of threads and GAMESS version from output as this may vary
grep -v 'num_threads' $output > tmp
grep -v 'gms_version' tmp > $output
rm tmp
../../dacdif  $output.save $output
../../dacdif  $testname.gms_job.dip.save gms_job.dip
../../dacdif  $testname.gms_job.chg.save gms_job.chg

/bin/rm -f mdin mdinfo mdcrd dummy restrt old.gms_job.*
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)
