#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

#$1=mpi => use mpi data exchange

../../check_TeraChem.x
if( $status > 0) then
  exit(0)
endif

set sander = "../../../bin/sander.MPI"
if( $?TESTsander ) then
    set sander = $TESTsander
endif

if( ! $?DO_PARALLEL ) then
    echo "This test (PIMD) can only run in parallel"
    echo "Parallelism within TeraChem will be on top. Exiting."
    exit 0
else
    set numprocs = `$DO_PARALLEL echo "Testing number of processors" | wc -l`
    if ( $numprocs != 2 ) then
	echo "Error: This test needs to run in parallel on 2 processors. Exiting."
	exit 0
    endif
endif

echo "Parallelism treated within TeraChem"

set mpi = 0
if ( $1 == 'mpi' ) then
  echo "Testing MPI-2 Client/Server interface"
  set mpi = 1
endif

cat > mdin <<EOF
TeraChem PIMD TEST

&cntrl
  ipimd  = 2
  irest  = 0,           !restart
  ntx    = 1,           !coordinates and velocities are read in
  cut    = 9999.9       !non-bonded interactions cutoff
  dt     = 0.0005,      !0.5fs time step
  ntb    = 0,           !no periodicity and PME off!
  ntt    = 4,           !turn off thermostat
  nchain = 4,
  ntpr   = 1,           !print details to log every 10 steps (every 5fs)
  ntwx   = 1,           !write coordinates to mdcrd every 10 steps (every 5fs)
  ntwr   = 1,           !write restart file at last step
  nstlim = 2,           !run for 2 steps (1 fs at dt=0.5fs)
  nscm   = 0,           !No removal of COM motion,
  ifqnt  = 1,
 /
 &qmmm
  qmmask    = '@*',
  qmcharge  = 0,
  qm_theory = 'EXTERN',
  verbosity = 0,
 /
 &tc
  method      = 'rhf',
  basis       = 'sto-3g',
  convthre    = 1.0E-06,
  mpi         = $mpi,
  ngpus       = 0,
 /
EOF

set output = pimd.mdout

cat > gf_pimd <<EOF
-O -p h2o.prmtop -c bead1.inpcrd -o bead1.mdout -r bead1.restrt -x bead1.mdcrd -inf bead1.mdinfo -pimdout $output
-O -p h2o.prmtop -c bead2.inpcrd -o bead2.mdout -r bead2.restrt -x bead2.mdcrd -inf bead2.mdinfo -pimdout $output
EOF

if ( $mpi ) then
  set basedir = `pwd`
  # we need to run each terachem instance in a subdirectory
  # otherwise scratch files get overwritten (in directory scr)
  mkdir 001
  cd 001
  mpirun -np 1 terachem -UseMPI -ID 'terachem_port.001' > tc_job.001.dat &
  cd $basedir
  mkdir 002
  cd 002
  mpirun -np 1 terachem -UseMPI -ID 'terachem_port.002' > tc_job.002.dat &
  cd $basedir
endif

touch dummy
$DO_PARALLEL $sander -ng 2 -groupfile gf_pimd < dummy || goto error
../../dacdif $output.save $output
../../dacdif  bead1.mdcrd.save bead1.mdcrd
../../dacdif  bead2.mdcrd.save bead2.mdcrd
# remove info about number of threads from output as this may vary
grep -v 'num_threads' bead1.mdout | grep -v '  mpi         =' > tmp
mv tmp bead1.mdout
../../dacdif bead1.mdout.save bead1.mdout
grep -v 'num_threads' bead2.mdout | grep -v '  mpi         =' > tmp
mv tmp bead2.mdout
../../dacdif bead2.mdout.save bead2.mdout

# give terachem time to shutdown
if ( $mpi) then
   sleep 1
endif
# remove temporary files
/bin/rm -f mdin gf_pimd bead*.mdinfo dummy bead*.restrt NHC.dat rm tc_job*
/bin/rm -r -f 001 002
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)
