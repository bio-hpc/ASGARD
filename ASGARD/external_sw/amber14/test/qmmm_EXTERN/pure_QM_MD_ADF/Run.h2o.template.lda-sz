#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

../../check_ADF_adf.x
if( $status > 0) then
  exit(0)
endif

set sander = "../../../bin/sander"
if( $?TESTsander ) then
    set sander = $TESTsander
endif

if( ! $?DO_PARALLEL ) then
    setenv DO_PARALLEL " "
else
    echo "This test not set up for parallel"
    echo "Parallelism handled within ADF"
    exit 0
endif

# check on how many CPU core ADF shall be running
if ( $?ADF_NCPUS) then
    if ( $ADF_NCPUS > 1 ) then
        echo "ADF will run on $ADF_NCPUS CPU cores"
    endif
else
    set ADF_NCPUS = 1
    echo "ADF will run on 1 CPU core"
    echo "(environment variable ADF_NCPUS not set)"
endif

cat > mdin <<EOF
Geometry optimization with ADF (LDA/SZ), 2 steps steepest descent
 &cntrl
  imin=1,     ! do a minimization
  maxcyc=2,   ! max 2 minimization steps
  ncyc=2 ,    ! switch to conjugate gradient after 2 steps of steepest descent
  drms=0.5,   ! RMS gradient convergence criterium 0.5 (kcal/mol)/A = 2.d-3 au
  cut=9999.0, ! non-bonded cutoff (irrelevant for now with pure QM)
  ntb=0,      ! no periodic boundary conditions
  ntpr=1,     ! print every minimization step
  ntwx=1,     ! write coordinates each step
  ntwr=2,     ! write restart file each 10 steps
  ioutfm = 1, ! NetCDF MDCRD.
  ifqnt=1     ! do QM/MM
 /
 &qmmm
  qmmask    ='@*',
  qmcharge  = 0,
  qm_theory ='EXTERN',
  verbosity = 0,
 /
 &adf
  xc = 'LDA',
  basis = 'SZ',
  integration = 4.0,
  scf_conv = 1.0D-06,
  use_template = 1,
  num_threads = $ADF_NCPUS,
 /
EOF

set output = h2o.template.lda-sz.out
cp template adf_job.tpl

touch dummy
$DO_PARALLEL $sander -O -p h2o.prmtop -c h2o.inpcrd -o $output < dummy || goto error

# remove info about number of threads from output as this may vary
grep -v 'num_threads' $output > tmp
mv tmp $output

../../dacdif  $output.save $output

/bin/rm -f mdin mdinfo mdcrd dummy restrt adf_job.* t21.* adf.t21 logfile
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)
