#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

../../check_TeraChem.x
if( $status > 0) then
  exit(0)
endif

set sander = "../../../bin/sander"
if( $?TESTsander ) then
    set sander = $TESTsander
endif

set can_do_mpi=0
if( `echo $sander | grep "MPI"` != '') then
 echo 'Using MPI version of sander'
 set can_do_mpi=1
endif

set mpi=0
if( ! $?DO_PARALLEL ) then
    set DO_PARALLEL=' '
  echo "Running test using file-based exchange (DO_PARALLEL not set)"
  echo "Parallelism treated within TeraChem"
else
  if( $can_do_mpi == 1 ) then
    echo "Running test using MPI data exchange (DO_PARALLEL is set)"
    echo "Parallelism treated within TeraChem"
    set mpi=1
    set numprocs = `$DO_PARALLEL ../../numprocs`
    if ( $numprocs > 1 ) then
	echo "This test requires >> mpirun -np 1 <<"
	echo "You specified >> mpirun -np $numprocs <<"
	exit 0
    endif
  else
    echo "Can not do an MPI run using serial version of sander."
    echo "Unset DO_PARALLEL to perform serial test"
    exit(1)
  endif
endif

cat > mdin <<EOF
2 steps NVE pure QM MD with TeraChem (dt=0.5fs, no shake)
 &cntrl
  imin   = 0,           !no minimization
  irest  = 1,           !restart
  ntx    = 5,           !coordinates and velocities are read in
  cut    = 9999.9       !non-bonded interactions cutoff
  dt     = 0.0005,      !0.5fs time step
  ntb    = 0,           !no periodicity and PME off!
  ntt    = 0,           !turn off thermostat
  ntpr   = 1,           !print details to log every 10 steps (every 5fs)
  ntwx   = 1,           !write coordinates to mdcrd every 10 steps (every 5fs)
  ntwr   = 2,           !write restart file at last step
  nstlim = 2,           !run for 2 steps (1 fs at dt=0.5fs)
  nscm   = 0,           !No removal of COM motion,
  ioutfm = 1,           !NetCDF MDCRD.
  ifqnt  = 1,
 /
 &qmmm
  qmmask    = '@*',
  qmcharge  = 0,
  qm_theory = 'EXTERN',
  verbosity = 0,
 /
 &tc
  basis  = '6-31g',
  method = 'blyp',
  charge_analysis = 'mulliken',
  dipole = 1,
  mpi    = $mpi,
  ngpus  = 0, 
 /
EOF

set test = h2o.blyp_6-31g_md
rm -f $test.out

echo "mpi=$mpi"
if ( $mpi == 1 ) then
 mpirun -np 1 terachem -UseMPI > tc_job.dat &
endif

touch dummy
$DO_PARALLEL $sander -O -p h2o.prmtop -c h2o.restrt -o $test.out < dummy || goto error

cat $test.out | grep -v 'mpi' | grep -v 'ngpus' | grep -v 'gpuids' | grep -v 'root_number' | grep -v 'executable' > foo
mv foo $test.out

../../dacdif -a 0.0002 $test.out.save $test.out
../../dacdif $test.tc_job.dip.save tc_job.dip
../../dacdif $test.tc_job.chg.save tc_job.chg

rm -f dummy mdin mdinfo mdcrd dummy restrt inpfile.xyz startfile \
  tc_job.inp tc_job.dat old.tc_job.* startfile mdin mdinfo mdcrd restrt
rm -fr scr
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)
