#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP Tests minimization with 3D-RISM.
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

echo "SANDER: 3D-RISM"

if( ! $?TESTsander ) set TESTsander = "../../../bin/sander"

if ! { ../checkrismunsupported.sh $TESTsander } exit 0

if( ! $?DO_PARALLEL ) then
        setenv DO_PARALLEL " "
else
        set numprocs=`echo $DO_PARALLEL | awk -f ../../numprocs.awk `
        if ( $numprocs > 2 ) then
            echo " DO_PARALLEL set to $DO_PARALLEL"
            goto toomany
        else if ( $?MP_PROCS)then
            if ($MP_PROCS > 3)then
#               --- For IBM AIX ---
                echo "    MP_PROCS set to "$MP_PROCS
                goto toomany
            endif
        endif
endif

cat > mdin <<EOF
Alanine dipeptide run
 &cntrl
    ntx=1, ntpr=2, ntwx=0,ntwr=0
    ioutfm=1
    imin=1 ,drms=1.   !weak convergence criteria
    maxcyc=7,ntmin=3
    ig=314159
    ntb=0
    irism=1
    cut=999,
 /
 &rism
    !a real minimization should use a much lower tolerance
    tolerance=1e-6
    ng3=64,64,64,
    solvbox=32d0,32d0,32d0,
    buffer = -1,
    solvcut=12d0
    verbose=1
 /
EOF

$DO_PARALLEL $TESTsander -O -i mdin -o ala.min.out \
   -p ala.parm7 -c ala.min.nc -xvv ../../../AmberTools/test/rism1d/spc-kh/spc.xvv.save \
   || goto error

$AMBERHOME/AmberTools/test/nab/checkrism3d -err 1e-6 ala.min.out.save ala.min.out
/bin/rm -f mdin mdinfo restrt
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)

toomany:
    echo " too many processors for this test, exiting"
    echo "============================================================"
    exit(0)

